P -> LDE
LDE -> LDE DE
LDE -> DE
DE -> DF
DE -> DT
T -> INTEGER
T -> CHAR
T -> BOOLEAN
T -> STRING
T -> IDU
DT -> TYPE IDD EQUALS ARRAY LEFT_SQUARE NU RIGHT_SQUARE OF T
DT -> TYPE IDD EQUALS STRUCT NB LEFT_BRACES DC RIGHT_BRACES
DT -> TYPE IDD EQUALS T
DC -> DC SEMI_COLON LI COLON T
DC -> LI COLON T
DF -> FUNCTION IDD NF LEFT_PARENTHESIS LP RIGHT_PARENTHESIS COLON T MF B
LP -> LP COMMA IDD COLON T
LP -> IDD COLON T
LP -> ''
B -> LEFT_BRACES LDV LS RIGHT_BRACES
LDV -> LDV DV
LDV -> DV
LDV -> ''
LS -> LS S
LS -> S
DV -> VAR LI COLON T SEMI_COLON
LI -> LI COMMA IDD
LI -> IDD
S -> IF LEFT_PARENTHESIS E RIGHT_PARENTHESIS MT S
S -> IF LEFT_PARENTHESIS E RIGHT_PARENTHESIS MT S ELSE ME S
S -> WHILE MW LEFT_PARENTHESIS E RIGHT_PARENTHESIS MT S
S -> DO MW S WHILE LEFT_PARENTHESIS E RIGHT_PARENTHESIS SEMI_COLON
S -> NB B
S -> LV MA EQUALS E SEMI_COLON
S -> BREAK SEMI_COLON
S -> CONTINUE SEMI_COLON
S -> RETURN E SEMI_COLON
E -> E AND L
E -> E OR L
E -> L
L -> L LESS_THAN R
L -> L GREATER_THAN R
L -> L LESS_OR_EQUAL R
L -> L GREATER_OR_EQUAL R
L -> L EQUAL_EQUAL R
L -> L NOT_EQUAL R
L -> R
R -> R PLUS Y
R -> R MINUS Y
R -> Y
Y -> Y TIMES F
Y -> Y DIVIDE F
Y -> F
F -> LV
F -> PLUS_PLUS LV
F -> MINUS_MINUS LV
F -> LV PLUS_PLUS
F -> LV MINUS_MINUS
F -> LEFT_PARENTHESIS E RIGHT_PARENTHESIS
F -> IDU MC LEFT_PARENTHESIS LE RIGHT_PARENTHESIS
F -> MINUS F
F -> NOT F
F -> TRU
F -> FALS
F -> CH
F -> ST
F -> NU
LE -> LE COMMA E
LE -> E
LE -> ''
LV -> LV DOT IDT
LV -> LV LEFT_SQUARE E RIGHT_SQUARE
LV -> IDU
TRU -> TRUE
FALS -> FALSE
CH -> CHARACTER
ST -> STRINGVAL
NU -> NUMERAL
IDT -> ID
IDU -> ID
IDD -> ID
NB -> ''
NF -> ''
MA -> ''
ME -> ''
MC -> ''
MF -> ''
MT -> ''
MW -> ''
